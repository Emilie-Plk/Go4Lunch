def openReport(htmlOutDir) {
    final reportPath = "$htmlOutDir\\index.html"

    println "HTML Report: $reportPath"

    def os = org.gradle.internal.os.OperatingSystem.current()
    if (os.isWindows()) {
        exec { commandLine 'cmd', '/c', "start $reportPath" }
    } else if (os.isMacOsX()) {
        exec { commandLine 'open', "$reportPath" }
    } else if (os.isLinux()) {
        try {
            exec { commandLine 'xdg-open', "$reportPath" }
        } catch (Exception ignored) {
            if (localProperties.containsKey("linux-html-cmd")) {
                exec { commandLine properties.get("linux-html-cmd"), "$reportPath" }
            } else {
                println "'linux-html-cmd' property could not be found in 'local.properties'"
            }
        }
    }
}

// JaCoCo stuff
android.applicationVariants.all { variant ->
    def variantName = variant.name.capitalize()
    def filesToAnalyze = [
            '**/*ViewModel.class',
            '**/*Repository*.class',
            '**/*UseCase.class'
    ]
    def filesToExclude = [
            '**/UserRepositoryFirestore.class',
            '**/NearbySearchRepositoryGooglePlaces.class',
            '**/GpsLocationRepositoryBroadcastReceiver.class',
            '**/ChatRepositoryFirestore.class',
            '**/AuthRepositoryFirebaseAuth.class',
            '**/AutocompleteRepositoryGooglePlaces.class',
            '**/DetailsRestaurantRepositoryGooglePlaces.class',
            '**/FavoriteRestaurantRepositoryFirestore.class',
    ]

    task("jacoco${variantName}Report", type: JacocoReport, dependsOn: "test${variantName}UnitTest") {
        group 'Reporting'
        description "Generate ${variantName} Jacoco coverage reports."

        def htmlOutDir = layout.buildDirectory.dir("reports/jacoco/$name/html").get().asFile

        doLast {
            openReport htmlOutDir
        }

        reports {
            xml.required = true
            html.outputLocation = htmlOutDir
        }

        getSourceDirectories().setFrom(files(android.sourceSets.main.java.srcDirs))
        getExecutionData().setFrom(files("${buildDir}/jacoco/test${variantName}UnitTest.exec"))
        getClassDirectories().setFrom(fileTree(dir: variant.javaCompileProvider.get().destinationDir, includes: filesToAnalyze, excludes: filesToExclude))
    }
}